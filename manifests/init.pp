class vim($user, $home_dir) {
  include wget 

  case $operatingsystem {
    CentOS,RedHat: { $vim_package = 'vim-enhanced' }
    default: { $vim_package = 'vim' }
  }

  package { 'vim':
    name   => $vim_package,
    ensure => installed,
  }

  file { ["${home_dir}/.vim","${home_dir}/.vim/autoload","${home_dir}/.vim/bundle"] : 
    ensure => 'directory',
    owner  => $user
  }

  wget::fetch { "DownloadPathogen":
    source      => "https://raw.github.com/tpope/vim-pathogen/master/autoload/pathogen.vim",
    destination => "${home_dir}/.vim/autoload/pathogen.vim",
    verbose     => true
  }

  file { "${home_dir}/.vim/autoload/pathogen.vim":
    owner => $user
  }

  concat { 'vimrc':
    path  => "${home_dir}/.vimrc",
    owner => $user,
    group => 'root',
    mode  => '0664',
  }

  Concat::Fragment {
    target  => 'vimrc',
  }

  concat::fragment { 'rc-header':
    target  => 'vimrc',
    content => "\" generated by Puppet module vim\n\n",
    order   => '05',
  }

  vim::rc { 'vimrc-pathogen':
    content => "execute pathogen#infect()\ncall pathogen#helptags()",
  }
  vim::rc { 'syntax on': }
  vim::rc { 'filetype plugin indent on': }
  vim::rc { 'highlight comment ctermfg=darkgray': }
  vim::rc { ':set bg=dark': }

  Package['vim'] 
  -> File["${home_dir}/.vim", "${home_dir}/.vim/autoload","${home_dir}/.vim/bundle"] 
  -> Wget::Fetch["DownloadPathogen"] 
  -> File["${home_dir}/.vim/autoload/pathogen.vim"] 
  -> Concat['vimrc']

}
